import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
}
apply from: 'gradle/properties.gradle'

group = info.group
version = "${info.version}-${version_spigot}"

sourceCompatibility = 8
targetCompatibility = 8

test {
    useJUnitPlatform()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT'
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT'
    testImplementation "com.github.MilkBowl:VaultAPI:1.7"

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.1"
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.mockito:mockito-inline:4.8.0'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [
                'version'    : version.toString(),
                'api-version': version_spigot_api.toString(),
        ]
    }
}

jar {
    exclude "META-INF/versions/9/module-info.class"
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}

task copyJarToServer(type: Copy) {
    from jar
    into "server/plugins"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

build.dependsOn copyJarToServer